buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
    }
}

allprojects {
    task eclipseSetup {
        description = "Cleans and generates eclipse project/classpath files that are not under version controlled."
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'findbugs'

    version = '0.2'

    eclipseSetup.dependsOn cleanEclipseProject
    eclipseSetup.dependsOn eclipseProject
    eclipseSetup.dependsOn cleanEclipseClasspath
    eclipseSetup.dependsOn eclipseClasspath

    repositories {
        jcenter()
        maven {
            url "https://github.com/juckele/juckele-mvn-repo/raw/master/releases"
        }
    }

    tasks.withType(FindBugs) {
        excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
    }

    dependencies {
    }
}

project(':vivarium-core') {
    dependencies {
        compile 'org.javatuples:javatuples:1.2'
        testCompile group: 'com.johnuckele', name: 'vtest', version: '0.9.2'
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }
}

project(':vivarium-scripts') {
    apply plugin: 'gradle-one-jar'

    task jarCreateAuditFunction( type: OneJar ) {
        archiveName = 'create-audit-function'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.CreateAuditFunction'
    }
    task jarCreateBlueprint( type: OneJar ) {
        archiveName = 'create-blueprint'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.CreateBlueprint'
    }
    task jarCreateSpecies( type: OneJar ) {
        archiveName = 'create-species'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.CreateSpecies'
    }
    task jarCreateWorld( type: OneJar ) {
        archiveName = 'create-world'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.CreateWorld'
    }
    task jarRunSimulation( type: OneJar ) {
        archiveName = 'run-simulation'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.RunSimulation'
    }

    task jarCollection {
        description = "Creates a complete set of stand-alone jars for scripts in this project."
    }
    jarCollection.dependsOn jarCreateAuditFunction
    jarCollection.dependsOn jarCreateBlueprint
    jarCollection.dependsOn jarCreateSpecies
    jarCollection.dependsOn jarCreateWorld
    jarCollection.dependsOn jarRunSimulation

    dependencies {
        compile project(':vivarium-core')
        compile 'commons-cli:commons-cli:1.3.1'
        compile 'org.json:json:20140107'
        testCompile group: 'com.johnuckele', name: 'vtest', version: '0.9.1'
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }
}

project(':vivarium-swing') {
    dependencies {
        compile project(':vivarium-core')
        compile project(':vivarium-scripts')
	compile 'net.java.dev.jna:jna-platform:4.0.0'
    }
}