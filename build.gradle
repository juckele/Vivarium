buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
    }
}

allprojects {
    task eclipseSetup {
        description = "Cleans and generates eclipse project/classpath files that are not under version controlled."
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'findbugs'
    apply plugin: 'jacoco'

    version = '0.3'

    eclipseSetup.dependsOn cleanEclipseProject
    eclipseSetup.dependsOn eclipseProject
    eclipseSetup.dependsOn cleanEclipseClasspath
    eclipseSetup.dependsOn eclipseClasspath

    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
        }
    }

    repositories {
        jcenter()
        maven {
            url "https://github.com/juckele/juckele-mvn-repo/raw/master/releases"
        }
    }

    tasks.withType(FindBugs) {
        excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
    }
}

project(':vivarium-core') {
    dependencies {
	compile 'com.googlecode.gwt-streamer:gwt-streamer:2.0.0'
        compile 'org.javatuples:javatuples:1.2'
        testCompile group: 'com.johnuckele', name: 'vtest', version: '0.9.2'
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }
}

project(':vivarium-serialization') {
    dependencies {
        compile project(':vivarium-core')
        testCompile group: 'com.johnuckele', name: 'vtest', version: '0.9.2'
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }
}

project(':vivarium-ga') {
    dependencies {
        compile project(':vivarium-core')
	compile project(':vivarium-serialization')
        testCompile group: 'com.johnuckele', name: 'vtest', version: '0.9.2'
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }
}

project(':vivarium-graph') {
    dependencies {
	compile 'jfree:jfreechart:1.0.9'
        testCompile group: 'com.johnuckele', name: 'vtest', version: '0.9.2'
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }
}

project(':vivarium-scripts') {
    apply plugin: 'gradle-one-jar'

    task jarConvertFileFormat( type: OneJar ) {
        archiveName = 'convert-file-format'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.ConvertFileFormat'
    }
    task jarCreateAuditFunction( type: OneJar ) {
        archiveName = 'create-audit-function'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.CreateAuditFunction'
    }
    task jarCreateBlueprint( type: OneJar ) {
        archiveName = 'create-blueprint'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.CreateBlueprint'
    }
    task jarCreateSpecies( type: OneJar ) {
        archiveName = 'create-species'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.CreateSpecies'
    }
    task jarCreateWorld( type: OneJar ) {
        archiveName = 'create-world'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.CreateWorld'
    }
    task jarExtractCreatureFromWorld( type: OneJar ) {
        archiveName = 'extract-creature-from-world'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.ExtractCreatureFromWorld'
    }
    task jarRunSimulation( type: OneJar ) {
        archiveName = 'run-simulation'+'-'+version+'.jar'
        mainClass = 'com.johnuckele.vivarium.scripts.RunSimulation'
    }

    task jarCollection {
        description = "Creates a complete set of stand-alone jars for scripts in this project."
    }
    jarCollection.dependsOn jarConvertFileFormat
    jarCollection.dependsOn jarCreateAuditFunction
    jarCollection.dependsOn jarCreateBlueprint
    jarCollection.dependsOn jarCreateSpecies
    jarCollection.dependsOn jarCreateWorld
    jarCollection.dependsOn jarExtractCreatureFromWorld
    jarCollection.dependsOn jarRunSimulation
    build.dependsOn jarCollection

    dependencies {
        compile project(':vivarium-core')
	compile project(':vivarium-serialization')
	compile project(':vivarium-graph')
        compile 'commons-cli:commons-cli:1.3.1'
        compile 'org.json:json:20140107'
        testCompile group: 'com.johnuckele', name: 'vtest', version: '0.9.1'
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }
}

project(':vivarium-swing') {
    dependencies {
        compile project(':vivarium-core')
        compile project(':vivarium-scripts')
	compile 'net.java.dev.jna:jna-platform:4.0.0'
    }
}

project(':vivarium-web') {
    apply plugin: 'gwt'
    apply plugin: 'war'

    gwt {
        gwtVersion='2.7.0'

    	modules 'com.johnuckele.vivarium.web.VivariumWeb'

	src += files(project(':vivarium-core').sourceSets.main.allJava.srcDirs)
    }

    dependencies {
        compile project(':vivarium-core')
	compile 'com.googlecode.gwt-streamer:gwt-streamer:2.0.0'
    }
}
